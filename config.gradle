// 供根目录的build.gradle使用
static def isMacOsX() {
    return org.gradle.internal.os.OperatingSystem.current().isMacOsX()
}

// 全局配置
ext {
    // 库group
    JITPACK_GROUP = "com.github.y1xian"
    RELEASE_MAVEN_VERSION = "3.0.0" // 正式发布的 version

    // maven 测试仓库的位置
    TEST_MAVEN_DIR = isMacOsX() ? '/Users/yyx/maven/maven_repo' : 'D:\\maven\\maven_repo'
    TEST_MAVEN_GROUP = "${JITPACK_GROUP}.Oh" // 测试插件的 group
    TEST_MAVEN_VERSION = "3.0.1-SNAPSHOT" // 测试的 version , 版本高于正式
    // 是否开启 maven 测试仓库（注意：开启后 https://jitpack.io/ 插件失效，只在本地测试时才开启）
    TEST_MAVEN_ENABLE = true
    // 是否开启 测试插件（控制是否使用本地测试插件，或是远端正式发布的插件）
    TEST_PLUGIN_ENABLE = false
    // 当前仓库版本 （upload本地版本，需要sync project）
    LIB_VERSION = TEST_MAVEN_ENABLE ? TEST_MAVEN_VERSION : RELEASE_MAVEN_VERSION

    // 统一仓库
    repositoriesExt = {
        maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url "https://jitpack.io" }
        maven { url 'http://developer.huawei.com/repo' }
        maven { url "https://raw.github.com/bmob/bmob-android-sdk/master" }
        // 本地仓库
        maven { url uri(TEST_MAVEN_DIR) }
        mavenCentral()
        google()
    }

    androidExt = [
            compileSdkVersion: 30,
            buildToolsVersion: '30.0.3',
            minSdkVersion    : 24,
            applicationId    : "com.yyxnb.oh",
            targetSdkVersion : 30,
            versionCode      : 1,
            versionName      : '2022.0509.1600',
            minifyEnabled    : false,
            shrinkResources  : false,
            zipAlignEnabled  : false
    ]

    compileOptionsExt_1_8 = {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // 代码编译打包成dex
    dexOptionsExt = {
        // 使用增量模式构建
        incremental true
        // 最大堆内存
        javaMaxHeapSize "8g"
        // 是否支持大工程模式
        jumboMode = true
        // 预编译 dex 库。在我们build的时候会快些，但clean时便会慢，默认开启
        preDexLibraries = true
        // 线程数
        threadCount = 8
    }

    // 发现代码质量问题
    lintOptionsExt = {
        // true--关闭lint报告的分析进度
        quiet true
        //  true--所有正式版构建执行规则生成崩溃的lint检查，如果有崩溃问题将停止构建
        checkReleaseBuilds true
        // true--错误发生后停止gradle构建
        abortOnError false
        // true--只报告error
        ignoreWarnings true
        // true--忽略有错误的文件的全/绝对路径(默认是true)
        absolutePaths true
        // true--检查所有问题点，包含其他默认关闭项
        checkAllWarnings true
        // true--所有warning当做error
        warningsAsErrors true
    }

    versions = [
            android_plugin: '3.4.3',

            appcompat     : "1.2.0",
            core          : "1.5.0",
            fragment      : "1.2.0",
            annotation    : "1.1.0",
            recyclerview  : "1.1.0",
            constraint    : "2.0.4",
            cardview      : "1.0.0",
            viewpager2    : "1.0.0",
            startup       : "1.0.0",
            collection    : "1.1.0",

            lifecycle     : '2.2.0',

            multidex      : '2.0.1',

            material      : '1.4.0'

    ]

    classpathExt = [
            android_gradle: [group: 'com.android.tools.build', name: 'gradle', version: versions.android_plugin],
    ]

    dependenciesExt = [
            appcompat                : [group: 'androidx.appcompat', name: 'appcompat', version: versions.appcompat],
            core                     : [group: 'androidx.core', name: 'core', version: versions.core],
            fragment                 : [group: 'androidx.fragment', name: 'fragment', version: versions.fragment],
            recyclerview             : [group: 'androidx.recyclerview', name: 'recyclerview', version: versions.recyclerview],
            cardview                 : [group: 'androidx.cardview', name: 'cardview', version: versions.cardview],
            annotation               : [group: 'androidx.annotation', name: 'annotation', version: versions.annotation_],
            constraint               : [group: 'androidx.constraintlayout', name: 'constraintlayout', version: versions.constraint],
            viewpager2               : [group: 'androidx.viewpager2', name: 'viewpager2', version: versions.viewpager2],

            startup                  : [group: 'androidx.startup', name: 'startup-runtime', version: versions.startup],

            lifecycle                : [group: 'androidx.lifecycle', name: 'lifecycle-extensions', version: versions.lifecycle],
            lifecycle_service        : [group: 'androidx.lifecycle', name: 'lifecycle-service', version: versions.lifecycle],
            lifecycle_java8          : [group: 'androidx.lifecycle', name: 'lifecycle-common-java8', version: versions.lifecycle],
            lifecycle_process        : [group: 'androidx.lifecycle', name: 'lifecycle-process', version: versions.lifecycle],

            multidex                 : [group: 'androidx.multidex', name: 'multidex', version: versions.multidex],

    ]

}